import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'idea'
    id 'java'
    id 'java-test-fixtures'
    id 'io.freefair.lombok' version "${plugin_lombok_version}"
}

repositories {
    mavenCentral()
}

group "${project_group}"
version "${project_version}"
description "${project_description}"

ext {
    // Dependency versions.
    slf4j_version = '2.0.9'
    logback_version = '1.3.6'
    junit_version = '5.10.1'
    assertj_version = '3.24.2'
}

dependencies {
    // Main dependencies (Api, Implementation, Compile, Runtime, Annotation).
    implementation("org.slf4j:slf4j-api:${slf4j_version}")
    implementation("ch.qos.logback:logback-classic:${logback_version}")

    // Dev dependencies (Api, Implementation, Compile, Runtime, Annotation).

    // Test dependencies (Api, Implementation, Compile, Runtime, Annotation).
    testImplementation(platform("org.junit:junit-bom:${junit_version}"))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation("org.assertj:assertj-core:${assertj_version}")

    // Test fixtures dependencies (Api, Implementation, Compile, Runtime, Annotation).
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of("${java_version}")
    }

    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.warnings true
    options.deprecation true
    options.compilerArgs = ['-Xlint:deprecation']
}

test {
    useJUnitPlatform()

    testLogging {
        events = [/*TestLogEvent.STARTED,*/ TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED]
        exceptionFormat TestExceptionFormat.FULL
        showStandardStreams true
    }

    ignoreFailures true
}
